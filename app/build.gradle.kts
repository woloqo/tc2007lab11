import java.util.Properties

plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.compose)
    alias(libs.plugins.kotlin.serialization)
    alias(libs.plugins.ksp)
    alias(libs.plugins.hilt)
}

// La dirección del servidor sale de local.properties, que no se sube al repo:
// cada quien apunta a SU servidor sin tocar el código.
//   avisos.api=http://10.0.2.2:8000/api/                      el emulador, a tu computadora (el valor por omisión)
//   avisos.api=https://algo.trycloudflare.com/api/            un teléfono de verdad, por el túnel
val local = Properties().apply {
    val archivo = rootProject.file("local.properties")
    if (archivo.exists()) archivo.inputStream().use { load(it) }
}
val apiUrl: String = local.getProperty("avisos.api") ?: "http://10.0.2.2:8000/api/"

android {
    namespace = "mx.tec.avisos"
    compileSdk = 37

    defaultConfig {
        applicationId = "mx.tec.avisos"
        minSdk = 24
        targetSdk = 37
        versionCode = 1
        versionName = "1.0"

        buildConfigField("String", "API_URL", "\"$apiUrl\"")

        // Quién corre las pruebas instrumentadas (las de src/androidTest) en el emulador.
        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release { isMinifyEnabled = false }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    buildFeatures {
        compose = true
        buildConfig = true
    }
}

dependencies {
    implementation(platform(libs.androidx.compose.bom))
    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.lifecycle.runtime.ktx)
    implementation(libs.androidx.lifecycle.runtime.compose)
    implementation(libs.androidx.lifecycle.viewmodel.compose)
    implementation(libs.androidx.activity.compose)
    implementation(libs.androidx.ui)
    implementation(libs.androidx.ui.graphics)
    implementation(libs.androidx.ui.tooling.preview)
    implementation(libs.androidx.material3)
    implementation(libs.androidx.material.icons.core)
    implementation(libs.androidx.navigation.compose)
    implementation(libs.retrofit)
    implementation(libs.retrofit.kotlinx.serialization)
    implementation(libs.kotlinx.serialization.json)
    implementation(libs.okhttp.logging)
    implementation(libs.androidx.datastore.preferences)
    implementation(libs.okhttp.sse)
    implementation(libs.androidx.work.runtime)

    // Hilt (Práctica 9): la librería, el generador de código, y sus piezas para ViewModel y WorkManager
    implementation(libs.hilt.android)
    ksp(libs.hilt.compiler)
    implementation(libs.androidx.hilt.lifecycle.viewmodel.compose)
    implementation(libs.androidx.hilt.work)
    ksp(libs.androidx.hilt.compiler)

    // Imágenes: Coil las baja y las pinta; ExifInterface lee cómo venía girada la foto
    implementation(libs.coil.compose)
    implementation(libs.coil.network.okhttp)
    implementation(libs.androidx.exifinterface)

    // Pruebas locales (src/test): corren en la JVM de tu computadora, sin emulador.
    testImplementation(libs.junit)

    // Pruebas instrumentadas (src/androidTest): corren en el emulador, con Android de verdad.
    androidTestImplementation(libs.androidx.test.ext.junit)
    androidTestImplementation(libs.androidx.test.runner)
    androidTestImplementation(libs.kotlinx.coroutines.test)

    androidTestImplementation(platform(libs.androidx.compose.bom))
    androidTestImplementation(libs.androidx.compose.ui.test.junit4)
    // Compose necesita una Activity vacía donde dibujar la pantalla que se prueba.
    debugImplementation(libs.androidx.compose.ui.test.manifest)

    androidTestImplementation(platform(libs.androidx.compose.bom))
    androidTestImplementation(libs.androidx.compose.ui.test.junit4)
    // Compose UI Test trae Espresso 3.5.0, que truena en Android reciente: busca
    // InputManager.getInstance(), que ya no existe. La 3.7.0 pide el InputManager al sistema.
    androidTestImplementation(libs.androidx.test.espresso.core)
    // Compose necesita una Activity vacía donde dibujar la pantalla que se prueba.
    debugImplementation(libs.androidx.compose.ui.test.manifest)
}
